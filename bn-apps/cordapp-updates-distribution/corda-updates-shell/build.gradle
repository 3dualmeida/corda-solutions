buildscript {
    ext.aetherVersion = '1.0.0.v20140518'
    ext.mavenVersion = '3.1.0'
    ext.maven_resolver_version = "1.1.2-SNAPSHOT"
    ext.slf4j_version = '1.7.25'
    ext.tinylog_version = '1.3.5'
    ext.mustache_version = '0.9.5'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
    }
}

apply plugin: 'kotlin'

sourceSets {
    main {
        resources {
            srcDir "config/dev"
        }
    }
    test {
        resources {
            srcDir "config/test"
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // transport over flows
    compile project(':bn-apps:cordapp-updates-distribution:corda-updates-core')

    // cli utils
    // TODO: this is a temporary hack until v3.3. is released
    compile "$corda_release_group:corda-tools-cliutils:4.0-SNAPSHOT"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"
    testCompile project(":bn-apps:cordapp-updates-distribution:corda-updates-test-utils")

    // SLF4J and TinyLog - convenient, small and fast logging libraries.
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"
    compile "org.tinylog:tinylog:$tinylog_version"
    compile "org.tinylog:slf4j-binding:$tinylog_version"

    // mustache for template processing
    compile "com.github.spullara.mustache.java:compiler:$mustache_version"
}


jar {
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    baseName = "corda-updates-shell"
    manifest {
        attributes(
            'Main-Class': 'net.corda.businessnetworks.cordaupdates.shell.CordaUpdatesCLIKt'
        )
    }
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}
compileKotlin {
    kotlinOptions {
        languageVersion = "1.2"
    }
}